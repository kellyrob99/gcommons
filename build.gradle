import java.text.SimpleDateFormat

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'


defaultTasks 'clean', 'build', 'install'

sourceCompatibility = 1.6
group               = 'com.goldin'
version             = '0.6-SNAPSHOT'

groovyVersion       = '1.8.0-beta-4'
antVersion          = '1.8.2'
springVersion       = '3.0.5.RELEASE'
junitVersion        = '4.8.2'
truezipVersion      = '6.8.2'


repositories {
    mavenRepo urls: 'http://evgeny-goldin.org/artifactory/repo/'
}

dependencies {
    groovy      "org.codehaus.groovy:groovy-all:$groovyVersion"
    compile     "org.codehaus.groovy:groovy-all:$groovyVersion",
                "org.springframework:spring-context:$springVersion",
                "de.schlichtherle:truezip:$truezipVersion",
                "org.apache.ant:ant:$antVersion",
                "org.apache.ant:ant-commons-net:$antVersion",
                "commons-net:commons-net:2.2",
                "org.slf4j:slf4j-api:1.6.1",
                "ch.qos.logback:logback-classic:0.9.28"
    testCompile "org.springframework:spring-test:$springVersion",
                "junit:junit:$junitVersion"
}


/**
 * http://gradle.org/0.9.2/docs/dsl/org.gradle.plugins.idea.IdeaModule.html
 */
ideaProject.javaVersion    = '1.6.0_24'
ideaModule {
    javaVersion     = ideaProject.javaVersion
    downloadSources = true
    downloadJavadoc = false
    outputDir       = new File( project.buildDir, 'idea' )
    testOutputDir   = new File( project.buildDir, 'idea/test' )
}


/**
 * http://gradle.org/0.9.2/docs/dsl/org.gradle.api.tasks.testing.Test.html
 * http://gradle.org/0.9.2/docs/javadoc/org/gradle/api/tasks/testing/Test.html
 */
test {
    test.testReportDir    = new File(( File ) project.buildDir, 'test/reports' )
    test.testResultsDir   = new File(( File ) project.buildDir, 'test/results' )
    test.systemProperties = System.properties.containsKey( 'slowTests' ) ? [ slowTests : 'true' ] : [:]
    test.setDebug( System.properties.containsKey( 'debug' ))
}


/**
 * http://gradle.org/0.9.2/docs/dsl/org.gradle.api.tasks.javadoc.Groovydoc.html
 */
def d    = new Date()
def time = new SimpleDateFormat( "HH:mm '(GMT'Z')'" ).format( d )
def date = new SimpleDateFormat( 'MMMM dd, yyyy' ).format( d )
groovydoc {
    destinationDir = new File(( System.getProperty( 'groovydocDir' ) ?: project.buildDir.path + '/groovydoc' ),
                              ( version.contains( '-SNAPSHOT' ) ? '' : version ))
    header = '<a href="http://evgeny-goldin.com/wiki/GCommons">evgeny-goldin.com/wiki/GCommons</a>'
    footer = '<a href="http://evgeny-goldin.com/">Evgeny Goldin</a>. GrovyDoc is generated at ' + "$time on $date." + """
<script type='text/javascript'>
  var _gaq = _gaq || [];
  _gaq.push(['_setAccount', 'UA-7925869-2']);
  _gaq.push(['_trackPageview']);
  (function() {
    var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
    ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
    var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
  })();
</script>
"""
    /**
     * http://jira.codehaus.org/browse/GRADLE-1391
     */
    link 'http://static.springsource.org/spring/docs/3.0.x/javadoc-api/', 'org.springframework.'
    link 'http://download.oracle.com/javase/6/docs/api/',                 'java.'
    link 'http://groovy.codehaus.org/api/',                               'groovy.', 'org.codehaus.groovy.'
    link 'http://commons.apache.org/net/apidocs/',                        'org.apache.commons.net.'
    link 'http://evgeny-goldin.org/javadoc/truezip/',                     'de.schlichtherle.'

    /**
     * "Groovydoc not creating external links in generated HTML"
     * http://gradle.1045684.n5.nabble.com/Groovydoc-not-creating-external-links-in-generated-HTML-td3315578.html
     * http://stackoverflow.com/questions/986297/how-do-i-reference-a-classpath-from-gradle-0-6
     * http://jira.codehaus.org/browse/GRADLE-1391
     */
    groovyClasspath = configurations.compile
}

/**
 * Adding sources artifact
 * http://gradle.1045684.n5.nabble.com/javadoc-and-source-jars-td3246213.html
 */
task sourcesJar( type: Jar, dependsOn: classes ) {
     classifier = 'sources'
     from sourceSets.main.allSource
}
artifacts { archives sourcesJar }
